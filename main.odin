/* TODO:
   - Generate a map with Simplex from core:math/noise - DONE
   - Draw it using simple chars first - DONE with colors :)
   - Draw it using RayLib - DONE
   - Generate random seed
   - Adapt it to use either own implementation of Perlin or Simplex from core:math/noise
*/

package main

import "core:math"
import "core:c"
import "core:fmt"
import "core:math/noise"
import tansi "core:terminal/ansi"

import rl "vendor:raylib"

TerrainMap :: struct {
	width, height: u64,
	tiles:         [dynamic]u8,
}

AnsiConsts :: struct {
	blue, darkGreen, yellow, lightGreen, reset: string,
}

ansi :: AnsiConsts {
	blue       = tansi.CSI + tansi.FG_BLUE + tansi.SGR,
	darkGreen  = tansi.CSI + tansi.FG_COLOR_24_BIT + ";54;105;45" + tansi.SGR,
	yellow     = tansi.CSI + tansi.FG_YELLOW + tansi.SGR,
	lightGreen = tansi.CSI + tansi.FG_BRIGHT_GREEN + tansi.SGR,
	reset      = tansi.CSI + tansi.RESET + tansi.SGR,
}

drawASCII :: proc(terrain: TerrainMap) {
	for _, i in terrain.tiles {
		switch terrain.tiles[i] {
		case 0 ..= 50:
			fmt.printf(ansi.blue + "~ " + ansi.reset)

		case 51 ..= 150:
			fmt.printf(ansi.lightGreen + "= " + ansi.reset)

		case 151 ..= 200:
			fmt.printf(ansi.darkGreen + "T " + ansi.reset)

		case 201 ..= 250:
			fmt.printf(ansi.yellow + "^ " + ansi.reset)
		}

		if cast(u64)(i + 1) % terrain.width == 0 {
			fmt.printf("\n")
		}
	}
}

getTileRayLibColor :: proc(tileVal: u8) -> rl.Color {
	switch tileVal {
	case 0 ..= 25: // Water
		return RL_BLUE

	case 26 ..= 125: // Plains
		return RL_LGREEN

	case 126 ..= 200: // Forest
		return RL_DGREEN

	case 201 ..= 250: // Mountains
		fallthrough
	case:
		return RL_BROWN
	}
}

RL_BLUE :: rl.Color{40, 157, 235, 255}
RL_LGREEN :: rl.Color{114, 212, 145, 255}
RL_DGREEN :: rl.Color{42, 115, 15, 255}
RL_BROWN :: rl.Color{112, 73, 11, 255}

main :: proc() {
	terrain := TerrainMap {
		width  = 50,
		height = 50,
	}
	terrain.tiles = make([dynamic]u8, 0, terrain.width * terrain.height)
	defer delete(terrain.tiles)

	seed: i64 = 20_110_920 // TODO: Generate a random seed every time
	zoomFactor: f64 = 10.0

	for x: u64 = 0; x < terrain.width; x += 1 {
		for y: u64 = 0; y < terrain.height; y += 1 {
			noiseVal := noise.noise_2d(seed, {cast(f64)x / zoomFactor, cast(f64)y / zoomFactor})
			append(&terrain.tiles, cast(u8)(((noiseVal + 1.0) / 2.0) * 250.0))
		}
	}

	drawASCII(terrain)

	screenWidth: c.int = 1920
	screenHeight: c.int = 1080

	tileWidth := cast(u64)math.ceil(cast(f64)screenWidth / cast(f64)terrain.width)
	tileHeight := cast(u64)math.ceil(cast(f64)screenHeight / cast(f64)terrain.height)
	// tileWidth: u64 = 32
	// tileHeight: u64 = 32

	rl.InitWindow(screenWidth, screenHeight, "raylib log callback")
	defer rl.CloseWindow()

	for !rl.WindowShouldClose() {
		rl.BeginDrawing()

		for _, i in terrain.tiles {
			tileColor := getTileRayLibColor(terrain.tiles[i])

			rl.DrawRectangle(
				posX = cast(c.int)(tileWidth * (cast(u64)i % terrain.width)),
				posY = cast(c.int)(tileHeight * (cast(u64)i / terrain.width)),
				width = cast(c.int)tileWidth,
				height = cast(c.int)tileHeight,
				color = tileColor,
			)
		}

		rl.EndDrawing()
	}
}
